;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;
;   JOYCE/ANNE v2.1.0: PCW emulator                                           ;
;                                                                             ;
;   Copyright (C) 1998-1999, John Elliott <jce@seasip.demon.co.uk>            ;
;                                                                             ;
;    This program is free software; you can redistribute it and/or modify     ;
;    it under the terms of the GNU Library General Public License as          ;
;    published by the Free Software Foundation; either version 2 of the       ;
;    License, or(at your option) any later version.                           ;
;                                                                             ;
;    This program is distributed in the hope that it will be useful,          ;
;    but WITHOUT ANY WARRANTY; without even the implied warranty of           ;
;    MERCHANTABILITY or FITNESS FOR A PARTICULAR PURPOSE.  See the            ;
;    GNU Library General Public License for more details.                     ;
;                                                                             ;
;    You should have received a copy of the GNU Library General Public        ;
;    License along with this program; if not, write to the Free Software      ;
;    Foundation, Inc., 675 Mass Ave, Cambridge, MA 02139, USA.                ;
;                                                                             ;
;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;
;
;CENPORT - divert printing (at BIOS level) into the CEN port
;
FCB	EQU	005Ch
FDOS	EQU	5
CR	EQU	0Dh
LF	EQU	0Ah
EOF	EQU	1Ah
;
;We start with protection against being run from DOS.
;
	org	0100h

BASE:	DEFB	0EBh,04h	;DOS protection... JMPS LABE
	EX	DE,HL
	JP	BEGIN
	DEFB	0B4h,09h	;DOS protection... MOV AH,9
	DEFB	0BAh
	DEFW	BVMES		;DOS protection... MOV DX,OFFSET BVMES
	DEFB	0CDh,021h	;DOS protection... INT 21h.
	DEFB	0CDh,020h	;DOS protection... INT 20h.

	DEFB	cr,'JOYCE CENPORT v'
	include	version.inc
	DEFB	' (c) John Elliott',cr,lf
	DEFB	'Date: '
	include	built.inc
	DEFB	cr,lf,eof
;
BEGIN:	SUB	A		;Filter out the 8080s
	JP	PE,BADVER
	LD	C,0Ch		;Filter out CP/M <3
	CALL	FDOS
	CP	30h
	JR	C,BADVER
	XOR	A
	DEFB	0EDh, 0FEh	;Get JOYCE version
	INC	A		;Must return A=0FFh for Joyce v1.00
	JR	NZ,BADVER
	LD	DE,0126h	;v1.26+?
	AND	A
	SBC	HL,DE
	JR	NC,JRTSO
BADVER:	LD	DE,BVMES	;Failed.
	LD	C,9
	JP	FDOS
;
JRTSO:	XOR	A	;A = 0 => called from CP/M
	CALL	MAIN
	RST	0
		
	DEFS	0178h-$
	DEFB	'JOYCE',1,0,0	;File signature at end of first record.
				;DB 'JOYCE',1;  2 expansion bytes.
;
MAIN:	LD	DE,80h
	LD	A,(DE)
	INC	DE
	LD	L,A
	LD	H,0
	ADD	HL,DE
	LD	(HL),0		;0-terminate the command line
	EX	DE,HL
OPTLP:	LD	A,(HL)
	INC	HL
	CP	'-'
	JR	Z,ISOPT
	CP	'['
	JR	Z,ISOPT
	CP	'/'
	JR	Z,ISOPT
	CP	'\'
	JR	Z,ISOPT
	OR	A
	JR	NZ,OPTLP

	JP	INSTALL
;
ISOPT:	LD	A,(HL)
	INC	HL
	OR	A
	JP	Z,INSTALL
	CP	'?'
	JR	Z,HELP
	CP	'H'
	JR	NZ,OPTLP
;
HELP:	LD	DE,HELP$
	JR	ETIME
;
INSTALL:
	LD	HL,(1)	;BIOS
	LD	L,0Fh	;BIOS LIST
	PUSH	HL
	POP	IX	;IX->BIOS LIST routine
	LD	(IX+0),0EDh
	LD	(IX+1),0FDh
	LD	(IX+2),0C9h
	LD	L,2Dh	;BIOS LSTST
	PUSH	HL
	POP	IX	;IX->BIOS LSTST routine
	LD	(IX+0),0EDh
	LD	(IX+1),0FDh
	LD	(IX+2),0C9h
	LD	DE,UPDP$
ETIME:	LD	C,9
	JP	FDOS
	
;
DATE:	DEFS	5
HELP$:	DEFB	'CENPORT v1.26 for JOYCE',cr,lf,lf
	DEFB	'Syntax:   CENPORT  to send printer output to CEN (even if CP/M has'
	DEFB	cr,lf
	DEFB	'                  not detected a Centronics add-on)'
	DEFB	cr,lf
	DEFB	'$'

UPDP$:	DEFB	'CENPORT installed. Printer output forced to CEN.',cr,lf,'$'
BVMES:	DEFB	'This program will only run in the JOYCE emulator v1.26+',cr,lf,'$'


	END
